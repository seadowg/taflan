defaultTasks 'clean', 'lintDebug', 'testDebug', 'connectedAndroidTest'

static def versionNumber() {
    def gitCount = "git rev-list HEAD --count".execute().text.trim()

    if (gitCount != "") {
        gitCount.toInteger()
    } else {
        0
    }
}

buildscript {
    ext.kotlin_version = '1.3.10'

    repositories {
        jcenter()
        mavenCentral()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    jcenter()
    mavenCentral()
    google()

    flatDir {
        dirs 'libs'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode versionNumber()
        versionName "1.0.${versionNumber()}"
        applicationId "com.seadowg.taflan"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    testOptions.unitTests.includeAndroidResources true
}

dependencies {

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.github.salomonbrys.kodein:kodein:3.3.0'
    implementation 'com.google.code.gson:gson:2.8.4'

    implementation 'org.apache.commons:commons-csv:1.3'

    // Android
    implementation 'com.google.android.material:material:1.0.0-rc01'
    implementation 'androidx.core:core-ktx:1.0.0-rc01'
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0-rc01"
    implementation 'androidx.annotation:annotation:1.0.0-rc01'

    // Robolectric
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation "org.robolectric:robolectric:4.1"
    testImplementation "com.nhaarman:mockito-kotlin:1.6.0"
    testImplementation 'org.assertj:assertj-core:3.9.0'

    // Espresso
    androidTestImplementation 'androidx.test:runner:1.1.0-alpha4'
    androidTestImplementation 'androidx.test:rules:1.1.0-alpha4'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha4')
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.0-alpha4'

    androidTestImplementation(name: 'macchiato-0.4.0', ext: 'aar')
}
